
! *** OPTICS=LCLS05JUN17 ***

! LCLS matching routines

  OPTION, -ECHO

! ==============================================================================
! SUBROUTINEs
! ------------------------------------------------------------------------------

  ML1c : SUBROUTINE

  ! match L1 phase advance per cell (coasting)

    VALUE, KQL1
    USE, L1c
    CELL
      VARY, KQL1, STEP=1.E-5, LOWER=0, UPPER=100
      CONSTR, #E, MUX=MU_L1, MUY=MU_L1
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.448192E-25   ! =expected penalty function with fitting OFF
    VALUE, KQL1
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML2c : SUBROUTINE

  ! match L2 phase advance per cell (coasting)

    VALUE, KQL2
    USE, L2c
    CELL
      VARY, KQL2, STEP=1.E-5, LOWER=0, UPPER=100
      CONSTR, #E, MUX=MU_L2, MUY=MU_L2
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 2.277840E-24   ! =expected penalty function with fitting OFF
    VALUE, KQL2
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML3c : SUBROUTINE

  ! match L3 phase advance per cell (coasting)

    VALUE, KQFL3,KQDL3
    USE, L3c
    CELL
      VARY, KQFL3, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQDL3, STEP=1.E-5, LOWER=-100, UPPER=0
      CONSTR, #E, MUX=MUX_L3, MUY=MUY_L3
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.809822E-13   ! =expected penalty function with fitting OFF
    VALUE, KQFL3,KQDL3
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MTWSSC : SUBROUTINE

  ! find Twiss at cathode
  ! (NOTE: optics between cathode and DL1beg is meaningless ... used only to
  !        initialize sumL and show beamline layout)

    VALUE, CBETX,CALFX,CBETY,CALFY
    USE, GUNL0a
    MATCH, ENERGY=E00, BETX=CBETX, ALFX=CALFX, BETY=CBETY, ALFY=CALFY
      VARY, CBETX, STEP=1.E-4, LOWER=0.1, UPPER=100
      VARY, CALFX, STEP=1.E-4, LOWER=-10, UPPER=10
      VARY, CBETY, STEP=1.E-4, LOWER=0.1, UPPER=100
      VARY, CALFY, STEP=1.E-4, LOWER=-10, UPPER=10
      CONSTR, #E, BETX=TBETX, ALFX=TALFX, BETY=TBETY, ALFY=TALFY
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE,  1.434054E-15   ! =expected penalty function with fitting OFF
    VALUE, CBETX,CALFX,CBETY,CALFY
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MGSPEC : SUBROUTINE

  ! match gun spectrometer

    VALUE, QG02[K1],QG03[K1]
    USE, (GUNBXG,GSPEC)
    MATCH, BETA0=TWSSC
      VARY, QG02[K1], STEP=1.E-4, LOWER=-100,  UPPER=+100
      VARY, QG03[K1], STEP=1.E-4, LOWER=-100,  UPPER=+100
      RMATRIX, YAG01/YAGG1, RM(1,2)=0, WEIGHT(1,2)=6, &
                            RM(3,4)=0, WEIGHT(3,4)=6
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 3.013681E-13   ! =expected penalty function with fitting OFF
    VALUE, QG02[K1],QG03[K1]
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBChETA : SUBROUTINE

  ! set Heater-chicane peak dispersion

    VALUE, BBh1,ABh1
    USE, (DL1)
    MATCH, ENERGY=Ei
      VARY, BBh1, STEP=1.E-5, LOWER=-8, UPPER=8
      CONSTR, HTRUND, DX=-0.035486*LSRHTR_ON    ! want 35 mm X-offset - need 35.486 mm of etaX to get it
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.088755E-20   ! =expected penalty function with fitting OFF
    VALUE, BBh1,ABh1
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBSYS : SUBROUTINE

  ! match DL1 bend system dispersion

    VALUE, KQB
    USE, (L0b,DL1)
    MATCH, BETA0=TWSS0
      VARY, KQB, STEP=1.E-5, LOWER=-39, UPPER=+39
      RMATRIX, DE09/BX02B, RM(1,6)=0
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 2.099758E-22   ! =expected penalty function with fitting OFF
    VALUE, KQB
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MED0 : SUBROUTINE

  ! match from exit of L0 into ED0

    VALUE, KQA01,KQA02,KQE01,KQE02,KQE03,KQE04
    USE, (GUNL0a,L0b,DL1)
    MATCH, BETA0=TWSSC
      VARY, KQA01, STEP=1.E-5, LOWER=-39,  UPPER=+39
      VARY, KQA02, STEP=1.E-5, LOWER=-39,  UPPER=+39
      VARY, KQE01, STEP=1.E-5, LOWER=-39,  UPPER=+39
      VARY, KQE02, STEP=1.E-5, LOWER=-39,  UPPER=+39
      VARY, KQE03, STEP=1.E-5, LOWER=-39,  UPPER=+39
      VARY, KQE04, STEP=1.E-5, LOWER=-39,  UPPER=+39
      CONSTR, MRK0, BETX=BMIN0,ALFX=0.,BETY=BMIN0,ALFY=0.
      CONSTR, HTRUND, BETX<12,BETX>8,BETY<12,BETY>8
      !LMDIF
      !MIGRAD
      !SIMPLEX
    ENDMATCH
    VALUE, 1.900871E-09   ! =expected penalty function with fitting OFF (htr fully ON)
    VALUE, KQA01,KQA02,KQE01,KQE02,KQE03,KQE04
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MED2L1 : SUBROUTINE

  ! match from ED0 through injection bend system to L1

    VALUE, KQM01,KQM02,KQM03,KQM04
    USE, (L0b,DL1,L1)
    MATCH, BETA0=TWSS0
      VARY, KQM01, STEP=1.E-5, LOWER=-39,UPPER=+39
      VARY, KQM02, STEP=1.E-5, LOWER=-39,UPPER=+39
      VARY, KQM03, STEP=1.E-5, LOWER=-39,UPPER=+39
      VARY, KQM04, STEP=1.E-5, LOWER=-39,UPPER=+39
      CONSTR, QA11[1], BETY=BET11, BETX=BET12
      CONSTR, QA12[1], BETY=BET12, BETX=BET11
      CONSTR, QM03[1], BETY<10
      CONSTR, OTR4, BETX<1.7, BETY>8.0
      !MIGRAD
      !SIMPLEX
    ENDMATCH
    VALUE,  8.576116E-02   ! =expected penalty function with fitting OFF
    VALUE, KQM01,KQM02,KQM03,KQM04
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MSPEC : SUBROUTINE

  ! match 135-MeV spectrometer

    VALUE, KQS01,KQS02,KQM01,KQM02
    USE, (L0b,DL1A,SPECBL)
    MATCH, BETA0=TWSS0
      VARY, KQS01, STEP=1.E-5, LOWER=-40,  UPPER=+40   ! nominal fit at YAGS2
      VARY, KQS02, STEP=1.E-5, LOWER=-40,  UPPER=+40   ! nominal fit at YAGS2
!!    VARY, KQM01, STEP=1.E-5, LOWER=-40,UPPER=+40     ! only use when 3-keV resolution on OTR required
!!    VARY, KQM02, STEP=1.E-5, LOWER=-40,UPPER=+40     ! only use when 3-keV resolution on OTR required
      WEIGHT, BETX=10                                  ! all fits
      CONSTR, YAGS2, BETX<0.120,BETY>5,BETY<12         ! nominal fit at YAGS2
!!    CONSTR, OTRS1, BETX<0.07,BETY>5,BETY<12          ! only use when 3-keV resolution on OTR required
      CONSTR, YAGS2, DX<-0.9*E0i/Ei                    ! nominal fit at YAGS2
!!    CONSTR, OTRS1, DX=-1.0*E0i/Ei                    ! only use when 3-keV resolution on OTR required
!!    CONSTR, DS2, BETX<35                             ! only use when 3-keV resolution on OTR required
!!    CONSTR, BPM12, BETY<35                           ! only use when 3-keV resolution on OTR required
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE,  1.377848E-02   ! =expected penalty function with fitting OFF
    VALUE, KQS01,KQS02,KQM01,KQM02
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC1R56 : SUBROUTINE

  ! match BC1 R56

    VALUE, BB11,AB11,AB11S,LB11S,AB11L,LB11L
    USE, BC1c
    MATCH, ENERGY=EBC1
      VARY, BB11, STEP=1.E-5, LOWER=-16, UPPER=0
      RMATRIX, #S/#E, RM(5,6)=0.03905
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.020712E-28   ! =expected penalty function with fitting OFF
    VALUE, BB11,AB11,AB11S,LB11S,AB11L,LB11L
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC1e : SUBROUTINE

  ! match through BC1 chicane into emittance measurement system

    VALUE, KQA12,KQ21201,KQM11,KQM12,KQM13
    USE, (L0b,DL1,L1,BC1)
    MATCH, BETA0=TWSS0
      VARY, KQA12,   STEP=1.E-5, LOWER=-100, UPPER=+100
      VARY, KQ21201, STEP=1.E-5, LOWER=-100, UPPER=0
      VARY, KQM11,   STEP=1.E-5, LOWER=-100, UPPER=+100
      VARY, KQM12,   STEP=1.E-5, LOWER=-100, UPPER=+100
      VARY, KQM13,   STEP=1.E-5, LOWER=-100, UPPER=+100
      CONSTR, BX14B, BETX<4.0, ALFX<-0.0
      CONSTR, WS12, BETX=BMIN1, ALFX=0, BETY=BMIN1, ALFY=0
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.520512E-13  ! =expected penalty function with fitting OFF
    VALUE, KQA12,KQ21201,KQM11,KQM12,KQM13
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC1L2 : SUBROUTINE

  ! match BC1 to L2

    VALUE, KQM14,KQM15,KQ21401,KQ21501
    USE, (L0b,DL1,L1,BC1,LI21)
    MATCH, BETA0=TWSS0
!!    VARY, KQ21301, STEP=1.E-5, LOWER=-10,UPPER=0  ! leave this quad OFF - unhook cables?
      VARY, KQM14,   STEP=1.E-5, LOWER=0  ,UPPER=10
      VARY, KQM15,   STEP=1.E-5, LOWER=-10,UPPER=0
      VARY, KQ21401, STEP=1.E-5, LOWER=0  ,UPPER=10
      VARY, KQ21501, STEP=1.E-5, LOWER=-10,UPPER=0
      CONSTR, QM15[1], BETY<BET21
      CONSTR, Q21401[1], BETX=BET21 !, BETY=BET22
      CONSTR, Q21501[1], BETY=BET21 !, BETX=BET22
      CONSTR, Q21601[1], BETX=BET21, BETY=BET22
      CONSTR, Q21701[1], BETY=BET21, BETX=BET22
      CONSTR, Q21801[1], BETX=BET21, BETY=BET22
      CONSTR, Q21901[1], BETY=BET21 !, BETX=BET22
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 9.653556E+00   ! =expected penalty function with fitting OFF
    VALUE, KQM14,KQM15,KQ21401,KQ21501
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML22 : SUBROUTINE

  ! match LI22 betas

    VALUE, KQ21801,KQ21901,KQ22201,KQ22301
    USE, (L0b,DL1,L1,BC1,L2)
    MATCH, BETA0=TWSS0
      VARY, KQ21801, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ21901, STEP=1.E-5, LOWER=-100, UPPER=0
      VARY, KQ22201, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ22301, STEP=1.E-5, LOWER=-100, UPPER=0
      CONSTR, Q22401[1], BETX=BET21, BETY=BET22
      CONSTR, Q22501[1], BETX=BET22, BETY=BET21
      CONSTR, Q22601[1], BETX=BET21, BETY=BET22
      CONSTR, Q22701[1], BETX=BET22, BETY=BET21
      CONSTR, Q22801[1], BETX=BET21, BETY=BET22
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 9.912298E-02    ! =expected penalty function with fitting OFF
    VALUE, KQ21801,KQ21901,KQ22201,KQ22301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML23 : SUBROUTINE

  ! match LI23 betas

    VALUE, KQ22801,KQ22901,KQ23201,KQ23301
    USE, (L0b,DL1,L1,BC1,L2)
    MATCH, BETA0=TWSS0
      VARY, KQ22801, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ22901, STEP=1.E-5, LOWER=-100, UPPER=0
      VARY, KQ23201, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ23301, STEP=1.E-5, LOWER=-100, UPPER=0
      CONSTR, Q23401[1], BETX=BET21, BETY=BET22
      CONSTR, Q23501[1], BETX=BET22, BETY=BET21
      CONSTR, Q23601[1], BETX=BET21, BETY=BET22
      CONSTR, Q23701[1], BETX=BET22, BETY=BET21
      CONSTR, Q23801[1], BETX=BET21, BETY=BET22
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.349422E-02   ! =expected penalty function with fitting OFF
    VALUE, KQ22801,KQ22901,KQ23201,KQ23301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML24 : SUBROUTINE

  ! match LI24 betas

    VALUE, KQ23801,KQ23901,KQ24201,KQ24301,KQ24401,KQ24501
    USE, (L0b,DL1,L1,BC1,L2)
    MATCH, BETA0=TWSS0
      VARY, KQ23801, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ23901, STEP=1.E-5, LOWER=-100, UPPER=0
      VARY, KQ24201, STEP=1.E-5, LOWER=0   , UPPER=100
      VARY, KQ24301, STEP=1.E-5, LOWER=-100, UPPER=0
      VARY, KQ24401, STEP=1.E-5, LOWER=0   , UPPER=100
!      VARY, KQ24501, STEP=1.E-5, LOWER=-100, UPPER=0
      CONSTR, Q23901[1], BETY<50
      CONSTR, Q24201[1], BETX=BET21 ! , BETY=BET22
      CONSTR, Q24301[1], BETY=BET21 ! , BETX=BET22
      CONSTR, Q24401[1], BETX=BET21 ! , BETY=BET22
      CONSTR, Q24501[1], BETY=BET21 ! , BETX=BET22
      CONSTR, Q24601[1], BETX=BET21 ! , BETY=BET22
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 8.109371E-03   ! =expected penalty function with fitting OFF and when KQ24501 set to post MBC2i-fit value
    VALUE, KQ23801,KQ23901,KQ24201,KQ24301,KQ24401,KQ24501
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC2R56 : SUBROUTINE

  ! match BC2 R56

    VALUE, BB21,AB21
    USE, BC2
    MATCH, ENERGY=EBC2
      VARY, BB21, STEP=1.E-6, LOWER=-18, UPPER=0
      RMATRIX, #S/#E, RM(5,6)=0.0247
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.337643E-18   ! =expected penalty function with fitting OFF
    VALUE, BB21,AB21
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC2i : SUBROUTINE

  ! match into BC2

    VALUE, KQ24501,KQ24601,KQ24701,KQM21
    USE, (L0b,DL1,L1,BC1,L2,BC2)
    MATCH, BETA0=TWSS0
      VARY, KQ24501, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ24601, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ24701, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQM21,   STEP=1.E-5 !, LOWER=0   , UPPER=100
      WEIGHT, BETX=10
      CONSTR, BX24B, BETX=5.0, ALFX=-0.5
      WEIGHT, BETX=0.1  , BETY=0.1
      CONSTR, Q24601[1] , BETY>10
      CONSTR, Q24701A[2], BETY<50
      CONSTR, Q24701A[2], BETX<50
      CONSTR, Q24901A[2], BETY<75
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE,  6.532261E-03   ! =expected penalty function with fitting OFF
    VALUE, KQ24501,KQ24601,KQ24701,KQM21
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBC2L3 : SUBROUTINE

  ! match BC2 to L3

    VALUE, KQM22,KQ24901,KQ25201,KQ25301,KQ25401,KQ25501
    USE, (L0b,DL1,L1,BC1,L2,BC2,LI25)
    MATCH, BETA0=TWSS0
      VARY, KQM22,   STEP=1.E-5  , LOWER=-0.65, UPPER=0
      VARY, KQ24901, STEP=1.E-5 !, LOWER=-10 , UPPER=0
      VARY, KQ25201, STEP=1.E-5 !, LOWER=0   , UPPER=10
      VARY, KQ25301, STEP=1.E-5 !, LOWER=-10 , UPPER=0
      VARY, KQ25401, STEP=1.E-5 !, LOWER=0   , UPPER=10
      VARY, KQ25501, STEP=1.E-5 !, LOWER=-10 , UPPER=0
      CONSTR, Q25201[1], BETX<60
      CONSTR, Q25301[1], BETY<64
      CONSTR, Q25401[1], BETX=BET31, BETY=BET32
      CONSTR, Q25501[1], BETX=BET33, BETY=BET34
      CONSTR, Q25601[1], BETX=BET31, BETY=BET32
      CONSTR, Q25701[1], BETX=BET33, BETY=BET34
      CONSTR, Q25801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.442784E-02   ! =expected penalty function with fitting OFF
    VALUE, KQM22,KQ24901,KQ25201,KQ25301,KQ25401,KQ25501
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML26 : SUBROUTINE

  ! match LI26 betas

    VALUE, KQ25801,KQ25901,KQ26201,KQ26301
    USE, (L0b,DL1,L1,BC1,L2,BC2,L3)
    MATCH, BETA0=TWSS0
      VARY, KQ25801, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ25901, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ26201, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ26301, STEP=1.E-5 !, LOWER=-100, UPPER=0
      CONSTR, Q26301[1], BETX=BET33, BETY=BET34
      CONSTR, Q26401[1], BETX=BET31, BETY=BET32
      CONSTR, Q26501[1], BETX=BET33, BETY=BET34
      CONSTR, Q26601[1], BETX=BET31, BETY=BET32
      CONSTR, Q26701[1], BETX=BET33, BETY=BET34
      CONSTR, Q26801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.522531E-02   ! =expected penalty function with fitting OFF
    VALUE, KQ25801,KQ25901,KQ26201,KQ26301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML27 : SUBROUTINE

  ! match LI27 betas

    VALUE, KQ26801,KQ26901,KQ27201,KQ27301
    USE, (L0b,DL1,L1,BC1,L2,BC2,L3)
    MATCH, BETA0=TWSS0
      VARY, KQ26801, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ26901, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ27201, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ27301, STEP=1.E-5 !, LOWER=-100, UPPER=0
      CONSTR, Q27301[1], BETX=BET33, BETY=BET34
      CONSTR, Q27401[1], BETX=BET31, BETY=BET32
      CONSTR, Q27501[1], BETX=BET33, BETY=BET34
      CONSTR, Q27601[1], BETX=BET31, BETY=BET32
      CONSTR, Q27701[1], BETX=BET33, BETY=BET34
      CONSTR, Q27801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 5.351428E-03   ! =expected penalty function with fitting OFF
    VALUE, KQ26801,KQ26901,KQ27201,KQ27301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML28 : SUBROUTINE

  ! match LI28 betas

    VALUE, KQ27801,KQ27901,KQ28201,KQ28301
    USE, (L0b,DL1,L1,BC1,L2,BC2,L3)
    MATCH, BETA0=TWSS0
      VARY, KQ27801, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ27901, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ28201, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ28301, STEP=1.E-5 !, LOWER=-100, UPPER=0
      CONSTR, Q28301[1], BETX=BET33, BETY=BET34
      CONSTR, Q28401[1], BETX=BET31, BETY=BET32
      CONSTR, Q28501[1], BETX=BET33, BETY=BET34
      CONSTR, Q28601[1], BETX=BET31, BETY=BET32
      CONSTR, Q28701[1], BETX=BET33, BETY=BET34
      CONSTR, Q28801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 2.676795E-03   ! =expected penalty function with fitting OFF
    VALUE, KQ27801,KQ27901,KQ28201,KQ28301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML29 : SUBROUTINE

  ! match LI29 betas

    VALUE, KQ28801,KQ28901,KQ29201,KQ29301
    USE, (L0b,DL1,L1,BC1,L2,BC2,L3)
    MATCH, BETA0=TWSS0
      VARY, KQ28801, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ28901, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ29201, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ29301, STEP=1.E-5 !, LOWER=-100, UPPER=0
      CONSTR, Q29301[1], BETX=BET33, BETY=BET34
      CONSTR, Q29401[1], BETX=BET31, BETY=BET32
      CONSTR, Q29501[1], BETX=BET33, BETY=BET34
      CONSTR, Q29601[1], BETX=BET31, BETY=BET32
      CONSTR, Q29701[1], BETX=BET33, BETY=BET34
      CONSTR, Q29801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.515926E-03   ! =expected penalty function with fitting OFF
    VALUE, KQ28801,KQ28901,KQ29201,KQ29301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML30 : SUBROUTINE

  ! match LI30 betas

    VALUE, KQ29801,KQ29901,KQ30201,KQ30301
    USE, (L0b,DL1,L1,BC1,L2,BC2,L3)
    MATCH, BETA0=TWSS0
      VARY, KQ29801, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ29901, STEP=1.E-5 !, LOWER=-100, UPPER=0
      VARY, KQ30201, STEP=1.E-5 !, LOWER=0   , UPPER=100
      VARY, KQ30301, STEP=1.E-5 !, LOWER=-100, UPPER=0
      CONSTR, Q30301[1], BETX=BET33, BETY=BET34
      CONSTR, Q30401[1], BETX=BET31, BETY=BET32
      CONSTR, Q30501[1], BETX=BET33, BETY=BET34
      CONSTR, Q30601[1], BETX=BET31, BETY=BET32
      CONSTR, Q30701[1], BETX=BET33, BETY=BET34
      CONSTR, Q30801[1], BETX=BET31, BETY=BET32
      !LMDIF
      !MIGRAD
    ENDMATCH
    VALUE, 1.694700E-01   ! =expected penalty function with fitting OFF
    VALUE, KQ29801,KQ29901,KQ30201,KQ30301
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MVBEND : SUBROUTINE

  ! Match dispersion in vertical bend module

  VALUE, KQVB
  USE, VBEND
  MATCH, ENERGY=Ef
    VARY, KQVB, STEP=1.E-5, LOWER=-1.65, UPPER=1.65
    RMATRIX, #S/#E, RM(3,6)=0, RM(4,6)=0
    TMATRIX, #S/#E, TM(5,6,6)=0, WEIGHT(5,6,6)=0
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 2.632497E-28   ! =expected penalty function with fitting OFF
  VALUE, KQVB
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MDL2M : SUBROUTINE

  ! find periodic beta's through bend cells

  USE, DL23
  VALUE, MBETX, MALFX, MBETY, MALFY
  MATCH, BETA0=TWSSM
    VARY, MBETX, STEP=1.E-5, UPPER=100, LOWER=0.01
    VARY, MALFX, STEP=1.E-5, UPPER=20 , LOWER=-20
    VARY, MBETY, STEP=1.E-5, UPPER=100, LOWER=0.01
    VARY, MALFY, STEP=1.E-5, UPPER=20 , LOWER=-20
    CONSTR, #E, BETX=MBETX, BETY=MBETY, ALFX=-MALFX, ALFY=-MALFY
    CONSTR, BPMDL3, BETX<5
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 2.248941E-09   ! =expected penalty function with DL23 fitting OFF
  VALUE, MBETX, MALFX, MBETY, MALFY
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MBSY : SUBROUTINE

  ! match L3 to LTU

    BEAM, ENERGY=E00
    USE, (GUNL0a,L0b,LCLS)
    VALUE, KQ30701,KQ30801,KQ50Q1,KQ50Q2
    MATCH, BETA0=TWSSC
      VARY, KQ30701, STEP=1.E-4, UPPER=0
      VARY, KQ30801, STEP=1.E-4, LOWER=0
      VARY, KQ50Q1 , STEP=1.E-4, LOWER=-0.335, UPPER=0 !17GeV
      VARY, KQ50Q2 , STEP=1.E-4, LOWER=0
      CONSTR, MM1, BETX=MBETX0, ALFX=MALFX0, BETY=MBETY0, ALFY=MALFY0
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
    VALUE, KQ30701,KQ30801,KQ50Q1,KQ50Q2
    BEAM, ENERGY=E00
    USE, (GUNL0a,L0b,LCLS)
    TWISS, BETA0=TWSSC, SAVE
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, RANGE=LI30BEG/MUWALL, &
      STYLE=100, SPLINE, FILE="LCLS"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, RANGE=LI30BEG/MM2, &
      STYLE=100, SPLINE, FILE="LCLS"
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  ML3DL2 : SUBROUTINE

  ! match L3 to LTU

    BEAM, ENERGY=E00
    USE, CU_HXR
    VALUE, KQ30701,KQ30801,KQ50Q1,KQ50Q2,KQ50Q3,KQ4,KQ5,KQ6,KQA0
    MATCH, BETA0=TWSSC
      VARY, KQ30701, STEP=1.E-4, UPPER=0
      VARY, KQ30801, STEP=1.E-4, LOWER=0
      VARY, KQ50Q1 , STEP=1.E-4, LOWER=-0.335, UPPER=0 !17GeV
      VARY, KQ50Q2 , STEP=1.E-4, LOWER=0
     !VARY, KQ50Q3 , STEP=1.E-4
     !VARY, KQ4    , STEP=1.E-4, LOWER=-0.949, UPPER=0.949
     !VARY, KQ5    , STEP=1.E-4, LOWER=-0.949, UPPER=0.949 !17 GeV
     !VARY, KQ6    , STEP=1.E-4, LOWER=-0.949, UPPER=0.949
     !VARY, KQA0   , STEP=1.E-4, LOWER=-0.949, UPPER=0.949
      CONSTR, MM1, BETX=MBETX0, ALFX=MALFX0, BETY=MBETY0, ALFY=MALFY0
      comment
      WEIGHT, ALFX=2.E-5, ALFY=2.E-5
      CONSTR, QVB2[1], ALFX=0, ALFY=0
      WEIGHT, BETX=1.E-5, BETY=1.E-5
      CONSTR, Q50Q1[1] , BETX<145, BETY<145
      CONSTR, Q50Q2[1] , BETX<145, BETY<145
      WEIGHT, BETX=1.E-5, BETY=1.E-5
      CONSTR, Q50Q3[1], BETX<145, BETY<105
      CONSTR, Q4[1]  , BETX<105, BETY<145
      CONSTR, Q5[1]  , BETX<105, BETY<105
      CONSTR, Q6[1]  , BETX<105, BETY<120
      CONSTR, QA0[1] , BETX<105, BETY<105
      CONSTR, QVM1[1], BETX<100, BETY<100
      CONSTR, QVM2[1], BETX<100, BETY<100
      CONSTR, QVB1[1], BETX<100, BETY<100
      CONSTR, QVB2[1], BETX<100, BETY<100
      CONSTR, QVB3[1], BETX<100, BETY<100
      CONSTR, QVM3[1], BETX<100, BETY<100
      endcomment
     !SIMPLEX, TOL=1.E-20, CALLS=5000
      LMDIF, TOL=1.E-20, CALLS=20000
      MIGRAD, TOL=1.E-20, CALLS=20000
    ENDMATCH
    VALUE, KQ30701,KQ30801,KQ50Q1,KQ50Q2,KQ50Q3,KQ4,KQ5,KQ6,KQA0
    BEAM, ENERGY=E00
    USE, CU_HXR
    TWISS, BETA0=TWSSC, SAVE
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, RANGE=LI30beg/MM1, &
      SPLINE, STYLE=100, FILE="LCLS"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, RANGE=LI30beg/MM2, &
      SPLINE, STYLE=100, FILE="LCLS"
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MDL2B : SUBROUTINE

  ! match DL2 dogleg dispersion

  USE, DL23
  VALUE, KQDL
  MATCH, BETA0=TWSSM
    VARY, KQDL, STEP=1.E-5, UPPER=1.04, LOWER=0
    RMATRIX, #S/#E, RM(1,6)=0, RM(2,6)=0
    TMATRIX, #S/#E, TM(5,6,6)=0, WEIGHT(5,6,6)=0
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 6.275200E-17   ! =expected penalty function with DL23 fitting OFF
  VALUE, KQDL
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MDL2T : SUBROUTINE

  ! Match with triplet between bend-cells

  USE, DOGLG2B
  VALUE, KQT1, KQT2
  MATCH, BETA0=TWSSM
    VARY, KQT1  , STEP=1.E-5, UPPER=1.65, LOWER=-1.65
    VARY, KQT2  , STEP=1.E-5, UPPER=1.65, LOWER=-1.65
    CONSTR,SS3,BETX=MBETX,BETY=MBETY,ALFX=MALFX,ALFY=MALFY
    !LMDIF
    !MIGRAD
    !SIMPLEX
  ENDMATCH
  VALUE, 2.154821E-09   ! =expected penalty function with DOGLG2B fitting OFF
  VALUE, KQT1, KQT2
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MECELL : SUBROUTINE

  ! Match FODO quad strength for 45-deg wire-to-wire phase adv.

  USE, ECELL
  VALUE, KQED2
  CELL
    VARY, KQED2, STEP=1.E-5, LOWER=0, UPPER=100
    CONSTR, #E, MUX=45/360
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 6.356059E-19   ! =expected penalty function with fitting OFF
  VALUE, KQED2
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MED2P : SUBROUTINE

  ! Find periodic beta's through LTU emit-diag section

  USE, EDSYS
  VALUE, BX1, AX1, BY1, AY1
  MATCH, ENERGY=Ef, BETX=BX1, ALFX=AX1, BETY=BY1, ALFY=AY1
    VARY, BX1, STEP=1.E-5, UPPER=100, LOWER=0.01
    VARY, AX1, STEP=1.E-5, UPPER=20 , LOWER=-20
    VARY, BY1, STEP=1.E-5, UPPER=100, LOWER=0.01
    VARY, AY1, STEP=1.E-5, UPPER=20 , LOWER=-20
    CONSTR, WS31, BETX=BX1, BETY=BY1
    CONSTR, WS32, BETX=BX1, BETY=BY1
    CONSTR, WS33, BETX=BX1, BETY=BY1
    CONSTR, WS34, BETX=BX1, BETY=BY1
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 1.334540E-14   ! =expected penalty function with fitting OFF
  VALUE, BX1, AX1, BY1, AY1
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MED2DL2 : SUBROUTINE

  ! Match between bend cells and emit-diag section

  USE, LTU
  VALUE, KQEM1, KQEM2, KQEM3, KQEM4
  MATCH, ENERGY=Ef, BETX=MBETX, ALFX=MALFX, BETY=MBETY, ALFY=MALFY
    VARY, KQEM1  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQEM2  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQEM3  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQEM4  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    CONSTR,WS31,BETX=BX1,BETY=BY1,ALFX=AX1,ALFY=AY1
    !!CONSTR,OTR33,BETX=12*DE3[L]/5,BETY=12*DE3[L]/5,ALFX=0,ALFY=0   ! for slice-emit on OTR33 with KQED2=0
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE,  1.711345E-12  ! =expected penalty function with fitting OFF
  VALUE, KQEM1, KQEM2, KQEM3, KQEM4
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MUNDP : SUBROUTINE

  ! find proper periodic betas in undulator

  USE, UND
  VALUE, UBETX, UALFX, UBETY, UALFY
  MATCH, BETA0=TWSSU
    VARY, UBETX, STEP=1.E-5, LOWER=0.1
    VARY, UALFX, STEP=1.E-5
    VARY, UBETY, STEP=1.E-5, LOWER=0.1
    VARY, UALFY, STEP=1.E-5
    CONSTR,#S/#E,PATTERN="MUQ",BETX<35.1,BETY<30.7   ! <beta>=30 m
!!    CONSTR,#S/#E,PATTERN="MUQ",BETX<15.5,BETY<15.0  ! <beta>=10 m
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 8.911117E-01   ! =expected penalty function with fitting OFF
  VALUE, UBETX, UALFX, UBETY, UALFY
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MUNDM : SUBROUTINE

  ! match betas at entrance to undulator

  USE, LTU
  VALUE, KQUM1, KQUM2, KQUM3, KQUM4
  MATCH, ENERGY=Ef, BETX=MBETX, ALFX=MALFX, BETY=MBETY, ALFY=MALFY
    VARY, KQUM1  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQUM2  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQUM3  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    VARY, KQUM4  , STEP=1.E-5, UPPER=0.85, LOWER=-0.85
    CONSTR, #E, BETX=UBETX, ALFX=UALFX, BETY=UBETY, ALFY=UALFY
    !LMDIF, TOL=1.E-20
    !MIGRAD, TOL=1.E-20
  ENDMATCH
  VALUE, 2.549013E-12   ! =expected penalty function with fitting OFF
  VALUE, KQUM1, KQUM2, KQUM3, KQUM4
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MDUMP : SUBROUTINE

  ! match e- dump

  USE, (UND,UNDEXIT,DUMPLINE)
  VALUE, KQUE1,KQUE2,KQDMP
  MATCH, BETA0=TWSSU
    VARY, KQUE1, STEP=1.E-5, UPPER=1.65/2, LOWER=-1.65/2
    VARY, KQUE2, STEP=1.E-5, UPPER=1.65/2, LOWER=-1.65/2
    VARY, KQDMP, STEP=1.E-5, UPPER=1.65/2, LOWER=-1.65/2
!!    VARY, Ddmpv, STEP=1.E-4, UPPER=5, LOWER=-5
    CONSTR, DSB0a/#E, BETY<150, BETX<350
    CONSTR, OTRDMP, DY>0.58, BETY<0.6, BETX>100
    CONSTR, #E, DY<0.45
    !LMDIF
    !MIGRAD
  ENDMATCH
  VALUE, 9.306033E-01   ! =expected penalty function with fitting OFF
  VALUE, KQUE1,KQUE2,KQDMP
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MHXRSS : SUBROUTINE

  ! match e- dump

    BEAM, ENERGY=Ef
    USE, CHICANE
    MATCH, BETX=1, BETY=1, SURVEY
      VARY BB4, STEP=1.E-6, LOWER=0
      RMATRIX, M1/M2, RM(5,6)=12.E-6
     !LMDIF, TOL=1.E-20
     !MIGRAD, TOL=1.E-20
    ENDMATCH
    VALUE, BB4
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MALINE : SUBROUTINE
    BEAM, ENERGY=E00
    USE, CU_ALINE
    SAVEBETA, TWm, DBMARK29
    TWISS, BETA0=TWSSC
    BEAM, ENERGY=TWm[ENERGY]
    SET, SETAL, 1
    USE, (BSYALINE,DPR2)
    VALUE, KQ10,KQ11,KQ19,KQ20,KQ27,KQ28,KQ30,KQ38
    MATCH, BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
      VARY, KQ10, STEP=1.E-6, LOWER=0
      VARY, KQ11, STEP=1.E-6, UPPER=0
      VARY, KQ19, STEP=1.E-6, LOWER=0
      VARY, KQ20, STEP=1.E-6, LOWER=0
      WEIGHT, ALFX=1.E-5, ALFY=1.E-5
      CONSTR, Q19[1], ALFX=0, ALFY=0
      CONSTR, ENDB, DX=0, DPX=0
     !LMDIF, TOL=1.E-20, CALLS=10000
     !MIGRAD, TOL=1.E-20, CALLS=10000
    ENDMATCH
    MATCH, BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
      VARY, KQ27, STEP=1.E-6, UPPER=0
     !VARY, KSQ,  STEP=1.E-6, LOWER=0
      VARY, KQ28, STEP=1.E-6, LOWER=0
      VARY, KQ30, STEP=1.E-6, UPPER=0
      VARY, KQ38, STEP=1.E-6, LOWER=0
      CONSTR, #E, BETX=745, ALFX=0, BETY=1070, ALFY=0 !200x200 um waist at 3PR2
      LMDIF, TOL=1.E-20, CALLS=10000
      MIGRAD, TOL=1.E-20, CALLS=10000
    ENDMATCH
    VALUE, KQ10,KQ11,KQ19,KQ20,KQ27,KQ28,KQ30,KQ38
    BEAM, ENERGY=TWm[ENERGY]
    SET, SETAL, 1
    USE, (BSYALINE,DPR2)
    PRINT, FULL
    TWISS, COUPLE, SAVE, TAPE="twiss.tape", &
      BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      SPLINE, STYLE=100, TITLE="BSY to A-line", FILE="LCLSA"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=RBETX,RBETY, &
      SPLINE, STYLE=100, TITLE="BSY to A-line", FILE="LCLSA"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=DX, VAXIS2=DY, &
      SPLINE, STYLE=100, TITLE="BSY to A-line", FILE="LCLSA"
    BEAM, ENERGY=TWm[ENERGY]
    SET, SETAL, 1
    USE, BSYALINE
    MATCH, BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
      RMATRIX, BEGBSYA/ENDB, RM(5,6)=0
    ENDMATCH
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  L2K1in : SUBROUTINE

  ! initialize L2 quad K1s to FODO cell values

    SET, KQ21401,  KQL2
    SET, KQ21501, -KQL2
    SET, KQ21601,  KQL2
    SET, KQ21701, -KQL2
    SET, KQ21801,  KQL2
    SET, KQ21901, -KQL2

    SET, KQ22201,  KQL2
    SET, KQ22301, -KQL2
    SET, KQ22401,  KQL2
    SET, KQ22501, -KQL2
    SET, KQ22601,  KQL2
    SET, KQ22701, -KQL2
    SET, KQ22801,  KQL2
    SET, KQ22901, -KQL2

    SET, KQ23201,  KQL2
    SET, KQ23301, -KQL2
    SET, KQ23401,  KQL2
    SET, KQ23501, -KQL2
    SET, KQ23601,  KQL2
    SET, KQ23701, -KQL2
    SET, KQ23801,  KQL2
    SET, KQ23901, -KQL2

    SET, KQ24201,  KQL2
    SET, KQ24301, -KQL2
    SET, KQ24401,  KQL2
    SET, KQ24501, -KQL2
    SET, KQ24601,  KQL2
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  L2K1out : SUBROUTINE

  ! output L2 quad K1s

    VALUE, KQ21401
    VALUE, KQ21501
    VALUE, KQ21601
    VALUE, KQ21701
    VALUE, KQ21801
    VALUE, KQ21901

    VALUE, KQ22201
    VALUE, KQ22301
    VALUE, KQ22401
    VALUE, KQ22501
    VALUE, KQ22601
    VALUE, KQ22701
    VALUE, KQ22801
    VALUE, KQ22901

    VALUE, KQ23201
    VALUE, KQ23301
    VALUE, KQ23401
    VALUE, KQ23501
    VALUE, KQ23601
    VALUE, KQ23701
    VALUE, KQ23801
    VALUE, KQ23901

    VALUE, KQ24201
    VALUE, KQ24301
    VALUE, KQ24401
    VALUE, KQ24501
    VALUE, KQ24601
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  L3K1in : SUBROUTINE

  ! initialize L3 quad K1s to FODO cell values

    SET, KQ25201, KQFL3
    SET, KQ25301, KQDL3
    SET, KQ25401, KQFL3
    SET, KQ25501, KQDL3
    SET, KQ25601, KQFL3
    SET, KQ25701, KQDL3
    SET, KQ25801, KQFL3
    SET, KQ25901, KQDL3

    SET, KQ26201, KQFL3
    SET, KQ26301, KQDL3
    SET, KQ26401, KQFL3
    SET, KQ26501, KQDL3
    SET, KQ26601, KQFL3
    SET, KQ26701, KQDL3
    SET, KQ26801, KQFL3
    SET, KQ26901, KQDL3

    SET, KQ27201, KQFL3
    SET, KQ27301, KQDL3
    SET, KQ27401, KQFL3
    SET, KQ27501, KQDL3
    SET, KQ27601, KQFL3
    SET, KQ27701, KQDL3
    SET, KQ27801, KQFL3
    SET, KQ27901, KQDL3

    SET, KQ28201, KQFL3
    SET, KQ28301, KQDL3
    SET, KQ28401, KQFL3
    SET, KQ28501, KQDL3
    SET, KQ28601, KQFL3
    SET, KQ28701, KQDL3
    SET, KQ28801, KQFL3
    SET, KQ28901, KQDL3

    SET, KQ29201, KQFL3
    SET, KQ29301, KQDL3
    SET, KQ29401, KQFL3
    SET, KQ29501, KQDL3
    SET, KQ29601, KQFL3
    SET, KQ29701, KQDL3
    SET, KQ29801, KQFL3
    SET, KQ29901, KQDL3

    SET, KQ30201, KQFL3
    SET, KQ30301, KQDL3
    SET, KQ30401, KQFL3
    SET, KQ30501, KQDL3
    SET, KQ30601, KQFL3
    SET, KQ30701, KQDL3
    SET, KQ30801, KQFL3
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  L3K1out : SUBROUTINE

  ! output L3 quad K1s

    VALUE, KQ25201
    VALUE, KQ25301
    VALUE, KQ25401
    VALUE, KQ25501
    VALUE, KQ25601
    VALUE, KQ25701
    VALUE, KQ25801
    VALUE, KQ25901

    VALUE, KQ26201
    VALUE, KQ26301
    VALUE, KQ26401
    VALUE, KQ26501
    VALUE, KQ26601
    VALUE, KQ26701
    VALUE, KQ26801
    VALUE, KQ26901

    VALUE, KQ27201
    VALUE, KQ27301
    VALUE, KQ27401
    VALUE, KQ27501
    VALUE, KQ27601
    VALUE, KQ27701
    VALUE, KQ27801
    VALUE, KQ27901

    VALUE, KQ28201
    VALUE, KQ28301
    VALUE, KQ28401
    VALUE, KQ28501
    VALUE, KQ28601
    VALUE, KQ28701
    VALUE, KQ28801
    VALUE, KQ28901

    VALUE, KQ29201
    VALUE, KQ29301
    VALUE, KQ29401
    VALUE, KQ29501
    VALUE, KQ29601
    VALUE, KQ29701
    VALUE, KQ29801
    VALUE, KQ29901

    VALUE, KQ30201
    VALUE, KQ30301
    VALUE, KQ30401
    VALUE, KQ30501
    VALUE, KQ30601
    VALUE, KQ30701
    VALUE, KQ30801
  ENDSUBROUTINE

! ==============================================================================
! COMMANDs
! ------------------------------------------------------------------------------

  BET11 := 10.090395936353
  BET12 := 2.470673595371
  BET21 := 43.876719659503
  BET22 := 16.019619746515
 !BET31 := 53.481287866029
 !BET32 := 35.468087644847
 !BET33 := 29.55797372867
 !BET34 := 63.601589300188
  BET31 := 61.868125327071
  BET32 := 36.40742728964
  BET33 := 36.353377790534
  BET34 := 61.956709274457
 !BET31 := 69.818359882868  ! for WS28 45 deg/wire
 !BET32 := 37.0062095289
 !BET33 := 42.5680425597
 !BET34 := 60.952049693321

  COMMENT
    ASSIGN, PRINT="LCLS.fit"

    COMMENT
      SET, MU_L1, 75/360
      ML1c
      USE, L1c
      SAVEBETA, TWSSL1, QFL1[1]
      TWISS
      SET, BET11, TWSSL1[BETX]
      SET, BET12, TWSSL1[BETY]
      VALUE, BET11,BET12
    ENDCOMMENT

    COMMENT
      SET, MU_L2, 55.500/360
      ML2c
      USE, L2c
      SAVEBETA, TWSSL2, QFL2[1]
      TWISS
      SET, BET21, TWSSL2[BETX]
      SET, BET22, TWSSL2[BETY]
      VALUE, BET21,BET22
    ENDCOMMENT

    COMMENT
     !SET, MUX_L3, 36.175/360  !July 13, 2005 - set for n*360 deg BX24A to BX31A (TCAV3 -> 25-2d)
      SET, MUX_L3, 30.175/360  !July 13, 2008 - set for best WS28 45-deg mux phase advances
      SET, MUY_L3, 30.130/360  !July 13, 2005 - set for 3*90 deg TCAV3h[1] to OTR30 (TCAV3 -> 25-2d)
      ML3c
      USE, L3c
      SAVEBETA, TWSSFL3, QFL3[1]
      SAVEBETA, TWSSDL3, QDL3[1]
      TWISS
      SET, BET31, TWSSFL3[BETX]
      SET, BET32, TWSSFL3[BETY]
      SET, BET33, TWSSDL3[BETX]
      SET, BET34, TWSSDL3[BETY]
      VALUE, BET31,BET32,BET33,BET34
    ENDCOMMENT

    MTWSSC
    MGSPEC
    MBChETA
    MBSYS
    MED0
    MED2L1
    MSPEC
    MBC1R56
    MBC1e
!**!L2K1in  !use ONLY when you're changing L2 phase advance per cell
    MBC1L2
    ML22
    ML23
    ML24
   !L2K1out
    MBC2R56
    MBC2i
!**!L3K1in  !use ONLY when you're changing L3 phase advance per cell
    MBC2L3
    ML26
    ML27
    ML28
    ML29
    ML30
   !L3K1out
    MVBEND
    MDL2M
    MBSY   !this one ...
   !ML3DL2 !... or this one
    MDL2B
    MDL2T
    MECELL
    MED2P
    MED2DL2
    MUNDP
    MUNDM
    MDUMP
    MHXRSS
  ENDCOMMENT

 !ML3DL2
 !MALINE

! ------------------------------------------------------------------------------

  COMMENT

  ! check I5 synchrotron integral for A-line ... for emittance growth

    BEAM, ENERGY=E00
    USE, CU_ALINE
    SAVEBETA, TWm, BEGBSYA
    TWISS, BETA0=TWSSC
    BEAM, ENERGY=TWm[ENERGY] ; VALUE, BEAM[ENERGY]
    SET, SETAL, 1
    USE, ALINE
    MATCH, BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
      WEIGHT, I5=1
      CONSTR, ENDB, I5=0
    ENDMATCH
    BEAM, ENERGY=TWm[ENERGY]
    SET, SETAL, 1
    USE, (ALINE,DPR2)
    PRINT, FULL
    TWISS, COUPLE, SAVE, &
      BETX=TWm[BETX], ALFX=TWm[ALFX], BETY=TWm[BETY], ALFY=TWm[ALFY]
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
      SPLINE=.T., STYLE=100, FILE="Aline"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=RBETX,RBETY, &
      SPLINE=.T., STYLE=100, FILE="Aline"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DX,DY, &
      SPLINE=.T., STYLE=100, FILE="Aline"
  ENDCOMMENT

! ------------------------------------------------------------------------------

  RETURN
